# 상태 파일 공유

<br>

## 버전 관리 시스템
여러 명의 팀원이 파일에 공통으로 액세스할 수 있게 하는 가장 일반적인 방법은 파일을 git과 같은 버전 관리 시스템에 두는 것  
그러나 Terraform 상태 파일을 버전 관리 시스템에 저장하는 것은 다음과 같은 이유 때문에 부적절

### 문제점
- **수동 오류**
  - Terraform을 실행하기 전에 최신 변경 사항을 가져오거나 실행하고 나서 푸시하는 것을 잊어버리면  
    팀의 누군가가 이전 버전의 상태 파일로 Terraform을 실행하고, 그 결과 실수로 이전 버전으로 롤백하거나  
    이전에 배포된 인프라를 복제하는 문제가 발생
- **잠금**
  - 대부분의 버전 관리 시스템은 여러 명의 팀 구성원이 동시에 하나의 상태 파일에 `terraform apply` 명령을 실행하지 못하게 하는 잠금 기능 미제공
- **시크릿**
  - Terraform 상태 파일의 모든 데이터는 평문으로 저장되는데 특정 Terraform 리소스에 중요한 데이터를 저장해야 할 때 문제 발생
  - 데이터베이스 사용자 이름과 비밀번호 같은 중요한 데이터를 평문으로 저장하는 것은 버전 관리나 보안 측면에서도 부적절

<br>

## 원격 백엔드
Terraform 백엔드는 Terraform이 상태를 로드하고 저장하는 방법을 결정

기본 백엔드는 로컬 백엔드로써 로컬 디스크에 상태 파일을 저장  
원격 백엔드(Amazon S3, Terraform Cloud 등)를 사용하면 상태 파일을 원격 공유 저장소에 저장 가능

### 버전 관리 시스템의 문제점 해결
- **수동 오류**
  - 원격 백엔드를 구성하면 Terraform은 plan이나 apply 명령을 실행할 때마다 해당 백엔드에서 상태 파일을 자동으로 로드
  - apply 명령을 실행한 후에는 상태 파일을 백엔드에 자동 저장하므로 수동 오류 문제 해결
- **잠금**
  - 대부분의 원격 백엔드는 기본적으로 잠금 기능 지원
  - `terraform apply` 명령을 실행하면 Terraform은 자동으로 잠금을 활성화  
    다른 사람이 apply 명령을 실행 중인 경우 이미 잠금이 활성화되어 있어 잠금이 해제될 때까지 대기
  - `-lock-timeout=<TIME>` 매개 변수를 사용하면 apply 명령을 실행할 때 잠금이 해제되기까지  
    Terraform이 얼마 동안 대기하도록 할지 설정 가능. `-lock-timeout=10m`으로 설정하면 10분 동안 대기
- **시크릿**
  - 대부분의 원격 백엔드는 기본적으로 데이터를 보내거나 상태 파일을 저장할 때 암호화하는 기능 지원  
    또한 이러한 백엔드는 Amazon S3 버킷과 IAM 정책을 사용하는 것 같이 일반적으로 액세스 제한을 구성하는 방법을 노출하므로  
    상태 파일에 액세스할 수 있는 사용자와 파일에 포함될 수 있는 시크릿 제어 가능

### Amazon S3
#### 장점
- **관리형 서비스**
  - 추가 인프라를 배포하고 관리하는 작업 불필요
- **높은 내구성 및 고가용성 제공**
  - 데이터 손실 또는 서비스 중단에 대한 걱정 불필요
- **암호화 지원**
  - 상태 파일에 민감한 데이터를 저장할 때 높은 안전성 보장
  - 전송 중인 데이터(Terraform은 S3에서 데이터를 읽고 저장할 때 SSL을 사용하여 암호화)와  
    저장되는 데이터(S3는 AES-256을 이용해 서버 측 암호화를 지원) 암호화 가능
- **DynamoDB를 통한 잠금 기능 지원**
- **버전 관리 지원**
  - 상태 파일의 수정 사항을 모두 저장
  - 문제 발생 시, 이전 버전으로 롤백 가능
- **대부분의 Terraform 기능을 프리 티어로 쉽게 사용 가능**
  - 비용 저렴

### 1. S3 bucket 생성
이전과 다른 디렉터리에 main.tf 파일을 생성하고 진행

main.tf
```terraform
provider "aws" {
  region = "us-east-2"
}

resource "aws_s3_bucket" "terraform_state" {
  bucket = "bigmtn1113-terraform-up-and-running-state"
  
  # 실수로 S3 버킷을 삭제하는 것을 방지
  lifecycle {
    prevent_destroy = true
  }
  
  # 코드 이력을 관리하기 위해 상태 파일의 버전 관리 활성화
  # 버킷의 파일이 업데이트될 때마다 새 버전 생성. 언제든지 이전 버전으로 롤백 가능
  versioning {
    enabled = true
  }
  
  # 서버 측 암호화 활성화
  # S3에 데이터가 저장될 때 상태 파일이나 그 파일에 포함된 모든 시크릿 암호화
  server_side_encryption_configuration {
    rule {
      apply_server_side_encryption_by_default {
        sse_algorithm = "AES256"
      }
    }
  }
}
```

### 2. DynamoDB table 생성
Amazon DynamoDB는 아마존의 분산형 키-값 저장소

분산 잠금 시스템에 필요한 강력한 읽기 일관성 및 조건부 쓰기 지원  
아마존이 완벽하게 관리하므로 직접 운영해야 하는 인프라가 없으며 대부분의 Terraform 기능이 프리 티어에 적용되므로 저렴하게 사용 가능

Terraform에서 DynamoDB를 잠금에 사용하려면 `LockID`라는 기본키가 있는 DynamoDB 테이블 생성 작업 필요

```terraform
resource "aws_dynamodb_table" "terraform_locks" {
  name         = "terraform-up-and-running-locks"
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "LockID"
  
  attribute {
    name = "LockID"
    type = "S"
  }
}
```

### 3. 공급자 코드 다운로드 후, 배포 진행
```bash
$ terraform init
$ terraform apply
```

※ 코드를 실행하기 위해선 다음과 같은 IAM 사용자 권한 필요
- AmazonS3FullAccess
- AmazonDynamoDBFullAccess
- AmazonRDSFullAccess

### 4. backend 구성
S3 버킷에 상태를 저장(암호화 및 잠금 사용)하도록 Terraform을 구성하려면 Terraform 코드 내에 backend 구성 설정 필요

```terraform
# backend 구성 구문

## BACKEND_NAME: 사용하려는 백엔드 이름
## CONFIG: S3 버킷 이름과 같이 해당 백엔드에 고유한 하나 이상의 인수로 구성

terraform {
  backend "<BACKEND_NAME>" {
    [CONFIG ...]
  }
}
```

```terraform
terraform {
  backend "s3" {
    # key는 Terraform 상태 파일을 저장할 S3 버킷 내의 파일 경로
    bucket = "bigmtn1113-terraform-up-and-running-state"
    key    = "global/s3/terraform.tfstate"
    region = "us-east-2"
    
    # encrypt를 통해 Terraform 상태 파일이 S3 디스크에 저장될 때 암호화
    dynamodb_table = "terraform-up-and-running-locks"
    encrypt        = true
  }
}
```

<br>

## 확인
### 1. terraform init
Terraform이 S3 버킷에 상태 파일을 저장하도록 지시하려면 `terraform init` 명령 실행 필요  
`terraform init`은 공급자 코드를 다운로드할 수 있을 뿐만 아니라 Terraform의 backend 구성도 가능

```bash
$ terraform init

Initializing the backend...
Acquiring state lock. This may take a few moments...
Do you want to copy existing state to the new backend?
  Pre-existing state was found while migrating the previous "local" backend to the
  newly configured "s3" backend. No existing state was found in the newly
  configured "s3" backend. Do you want to copy this state to the new "s3"
  backend? Enter "yes" to copy and "no" to start with an empty state.

  Enter a value: yes

Releasing state lock. This may take a few moments...

Successfully configured the backend "s3"! Terraform will automatically
use this backend unless the backend configuration changes.
```

Terraform은 로컬 디스크에 이미 상태 파일이 있음을 자동으로 감지하여 새로운 S3 백엔드에 복사한다는 메시지를 표시  
명령어를 실행하면 Terraform 상태가 S3 버킷에 저장

![image](https://user-images.githubusercontent.com/46125158/208114913-ac8f286a-d237-4a4e-9e57-132d46779e32.png)

백엔드가 활성화되면 Terraform은 명령을 실행하기 전에 S3 버킷에서 최신 상태를 자동으로 가져오고  
명령을 실행한 후에는 최신 상태를 S3 버킷으로 자동으로 푸시

### 2. output 작성
outputs.tf  
```terraform
output "s3_bucket_arn" {
  value       = aws_s3_bucket.terraform_state.arn
  description = "The ARN of the S3 bucket"  
}

output "dynamodb_table_name" {
  value       = aws_dynamodb_table.terraform_locks.name
  description = "The name of the DynamoDB table"
}
```

### 3. terraform apply
```bash
$ terraform apply

Acquiring state lock. This may take a few moments...

aws_dynamodb_table.terraform_locks: Refreshing state... [id=terraform-up-and-running-locks]
aws_s3_bucket.terraform_state: Refreshing state... [id=bigmtn1113-terraform-up-and-running-state]

Releasing state lock. This may take a few moments...

Apply complete! Resources: 0 added, 0 changed, 0 destroyed.

Outputs:

dynamodb_table_name = "terraform-up-and-running-locks"
s3_bucket_arn = "arn:aws:s3:::bigmtn1113-terraform-up-and-running-state"
```

Terraform이 apply 명령어를 실행하기 전 상태 파일 잠금을 실행하고, 명령어가 실행되고 나면 잠금을 해제

S3 versioning 확인  
![image](https://user-images.githubusercontent.com/46125158/208118677-f7cc33b6-a3bc-4336-8a24-0067aae292a3.png)

Terraform은 S3에 상태 데이터를 자동으로 푸시하거나 가져오고 S3기 상태 파일의 모든 변경 사항을 저장  
문제가 발생하는 경우 이전 버전으로 디버깅하고 롤백하기에 용이
