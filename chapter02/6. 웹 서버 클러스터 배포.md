# 웹 서버 클러스터 배포

<br>

하나뿐인 서버가 충돌하거나 트래픽 과부하가 발생하면 사용자는 사이트에 액세스 불가  
단일 서버가 아니라 서버 클러스터를 구성해서 트래픽을 분산시키고, 트래픽 양에 따라 클러스터의 크기를 늘리거나 줄임으로써 해결

<br>

## Auto Scaling Group
Auto Scaling Group(ASG)는 EC2 인스턴스 클러스터 시작, 각 인스턴스 상태 모니터링, 실패한 인스턴스 교체,  
로드에 따른 클러스터 사이즈 조정 등 많은 작업을 자동으로 처리 가능

### Launch configuration
ASG에서 각 EC2 인스턴스를 어떻게 구성할 것인지 설정

main.tf 파일에 launch configuration 내용 추가  
```terraform
resource "aws_launch_configuration" "example" {
  image_id        = "ami-0fb653ca2d3203ac1"
  instance_type   = "t2.micro"
  security_groups = [aws_security_group.instance.id]

  user_data = <<-EOF
              #!/bin/bash
              echo "Hello, World" > index.html
              nohup busybox httpd -f -p ${var.server_port} &
              EOF
}
```

시작 구성을 작성했으면 ASG 자체 생성 가능  
main.tf 파일에서 ASG 내용 추가  
```terraform
resource "aws_autoscaling_group" "example" {
  launch_configuration = aws_launch_configuration.example.name

  min_size = 2
  max_size = 10

  tag {
    key                 = "Name"
    value               = "terraform-asg-example"
    propagate_at_launch = true
  }
}
```

※ propagate_at_launch  
ASG를 통해 시작된 Amazon EC2 인스턴스에 태그 전파

#### Lifecycle
시작 구성은 변경할 수 없으므로 시작 구성의 매개 변수를 변경하면 Terraform이 이를 대체하려고 시도  
일반적으로 리소스를 교체할 때 Terraform은 이전 리소스를 먼저 삭제한 다음 대체 리소스를 생성  
그러나 ASG에 이전 리소스에 대한 참조가 있으므로 Terraform이 해당 리소스 삭제 불가

모든 Terraform 리소스는 리소스 생성, 업데이트 및 삭제 방법을 구성하는 몇 가지 수명 주기 설정을 지원  

`create_before_destroy`를 `true`로 설정하면 Terraform은 리소스를 교체하는 순서를 반대로 하여  
교체 리소스를 먼저 생성하고 기존 리소스를 삭제

main.tf 파일의 launch configuration 부분에 lifecycle 내용 추가  
```terraform
resource "aws_launch_configuration" "example" {
  image_id        = "ami-0fb653ca2d3203ac1"
  instance_type   = "t2.micro"
  security_groups = [aws_security_group.instance.id]

  user_data = <<-EOF
              #!/bin/bash
              echo "Hello, World" > index.html
              nohup busybox httpd -f -p ${var.server_port} &
              EOF

  # ASG에서 시작 구성을 사용할 때 필요
  lifecycle {
    create_before_destroy = true
  }
}
```

### data source
Terraform을 실행할 때마다 공급자에서 가져오는 읽기 전용 정보

Terraform 구성에 데이터 소스를 추가해도 새로운 것이 생성되지 않는데 이는 단순히 데이터 공급자의 API만 물어보고  
해당 데이터를 나머지 Terraform 코드에서 사용할 수 있도록 하는 방법

```terraform
# 데이터 소스 구문

## PROVIDER: 공급자 이름
## TYPE: 데이터 소스 유형
## NAME: Terraform 코드에서 데이터 소스를 참조하는 데 사용하는 식별자
## CONFIG: 해당 데이터 소스에 고유한 하나 이상의 인수로 구성

data "<PROVIDER>_<TYPE>" "<NAME>" {
  [CONFIG ...]
}
```

main.tf 파일에 데이터 소스 내용 추가  
```terraform
data "aws_vpc" "default" {
  default = true
}
```

데이터 소스에서 전달하는 인수는 일반적으로 원하는 정보를 데이터 소스에 표시하는 검색 필터  
`aws_vpc` 데이터 소스에 필요한 유일한 필터는 `default = true`이며, Terraform이 AWS 계정에서 기본 VPC를 찾도록 지시

#### data soucre 속성 참조
```terraform
# 데이터 소스 속성 참조 구문
data.<PROVIDER>_<TYPE>.<NAME>.<ATTRIBUTE>
```

main.tf 파일에 데이터 소스 내용 추가  
```terraform
# 기본 VPC 내 subnets 조회
data "aws_subnet_ids" "default" {
  vpc_id = data.aws_vpc.default.id
}
```

main.tf 파일의 ASG 부분에 subnets 내용 추가  
```terraform
resource "aws_autoscaling_group" "example" {
  launch_configuration = aws_launch_configuration.example.name
  vpc_zone_identifier  = data.aws_subnet_ids.default.ids

  min_size = 2
  max_size = 10

  tag {
    key                 = "Name"
    value               = "terraform-asg-example"
    propagate_at_launch = true
  }
}
```

**※ 참조 과정**  
기본 VPC ID 조회 -> 기본 VPC 내의 subnets ID 조회 -> ASG가 해당 subnets에 EC2를 배포하도록 설정
