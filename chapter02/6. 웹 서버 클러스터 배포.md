# 웹 서버 클러스터 배포

<br>

하나뿐인 서버가 충돌하거나 트래픽 과부하가 발생하면 사용자는 사이트에 액세스 불가  
단일 서버가 아니라 서버 클러스터를 구성해서 트래픽을 분산시키고, 트래픽 양에 따라 클러스터의 크기를 늘리거나 줄임으로써 해결

<br>

## Auto Scaling Group
Auto Scaling Group(ASG)는 EC2 인스턴스 클러스터 시작, 각 인스턴스 상태 모니터링, 실패한 인스턴스 교체,  
로드에 따른 클러스터 사이즈 조정 등 많은 작업을 자동으로 처리 가능

### Launch configuration
ASG에서 각 EC2 인스턴스를 어떻게 구성할 것인지 설정

main.tf 파일에서 다음과 같이 내용 추가  
```terraform
resource "aws_launch_configuration" "example" {
  image_id        = "ami-0fb653ca2d3203ac1"
  instance_type   = "t2.micro"
  security_groups = [aws_security_group.instance.id]

  user_data = <<-EOF
              #!/bin/bash
              echo "Hello, World" > index.html
              nohup busybox httpd -f -p ${var.server_port} &
              EOF
}
```

시작 구성을 작성했으면 ASG 자체 생성 가능  
main.tf 파일에서 다음과 같이 내용 추가  
```terraform
resource "aws_autoscaling_group" "example" {
  launch_configuration = aws_launch_configuration.example.name

  min_size = 2
  max_size = 10

  tag {
    key                 = "Name"
    value               = "terraform-asg-example"
    propagate_at_launch = true
  }
}
```

#### Lifecycle
시작 구성은 변경할 수 없으므로 시작 구성의 매개 변수를 변경하면 Terraform이 이를 대체하려고 시도  
일반적으로 리소스를 교체할 때 Terraform은 이전 리소스를 먼저 삭제한 다음 대체 리소스를 생성  
그러나 ASG에 이전 리소스에 대한 참조가 있으므로 Terraform이 해당 리소스 삭제 불가

모든 Terraform 리소스는 리소스 생성, 업데이트 및 삭제 방법을 구성하는 몇 가지 수명 주기 설정을 지원  

`create_before_destroy`를 `true`로 설정하면 Terraform은 리소스를 교체하는 순서를 반대로 하여  
교체 리소스를 먼저 생성하고 기존 리소스를 삭제

main.tf 파일에 lifecycle 내용 추가  
```terraform
resource "aws_launch_configuration" "example" {
  image_id        = "ami-0fb653ca2d3203ac1"
  instance_type   = "t2.micro"
  security_groups = [aws_security_group.instance.id]

  user_data = <<-EOF
              #!/bin/bash
              echo "Hello, World" > index.html
              nohup busybox httpd -f -p ${var.server_port} &
              EOF

  # ASG에서 시작 구성을 사용할 때 필요
  lifecycle {
    create_before_destroy = true
  }
}
```
