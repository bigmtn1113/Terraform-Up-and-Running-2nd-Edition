# 로드 밸런서 배포

<br>

각각 고유한 IP 주소를 가진 서버가 여러 개 있지만 사용자에게는 일반적으로 하나의 IP 주소를 제공  
로드 밸런서를 배포하여 서버 전체에 트래픽을 분산시키고 모든 사용자에게 로드 밸런서 IP를 제공

<br>

## 로드 밸런서 유형
- **Application Load Balancer(ALB)**
  - HTTP 및 HTTPS 트래픽 처리에 적합한 로드 밸런서
  - OSI 모델의 응용(7) 계층에서 작동
- **Network Load Balancer(NLB)**
  - TCP, UDP 및 TLS 트래픽 처리에 적합한 로드 밸런서
  - ALB보다 빠르게 로드에 응답하여 확장 및 축소 가능
  - 초당 수천만 개의 요청 처리 가능
  - OSI 모델의 전송(4) 계층에서 작동
- **Classic Load Balancer(CLB)**
  - '레거시' 로드 밸런서
  - HTTP, HTTPS, TCP 및 TLS 트래픽 처리가 가능하나, ALB 또는 NLB 보다 기능을 적게 보유
  - OSI 모델의 응용(7) 계층 및 전송(4) 계층에서 모두 작동

대부분의 응용 프로그램은 ALB 또는 NLB를 사용하며 현재 학습 예제는 HTTP 앱이므로 ALB가 가장 적합

<br>

## Application Load Balancer
### 구성
- **리스너(Listener)**
  - 80 같은 특정 포트와 HTTP 같은 프로토콜 수신
- **리스너 규칙(Listener rule)**
  - 리스너에 들어오는 요청을 가져와 /foo 및 /bar 같은 특정 경로나  
    foo.example.com 및 bar.example.com 같은 호스트 이름과 일치하는 요청을 특정 대상 그룹으로 전송
- **대상 그룹(Target groups)**
  - 로드 밸런서에서 요청을 받는 하나 이상의 서버
  - 서버의 상태를 확인하고 요청을 정상 노드로 전송

### ALB 생성
AWS 로드 밸런서는 단일 서버가 아니라 별도의 서브넷(및 별도의 데이터 센터)에서 실행될 수 있는 여러 서버로 구성  
AWS는 트래픽에 따라 로드 밸런서 서버 수를 자동으로 확장 또는 축소하고  
해당 서버 중 하나가 다운되면 장애 조치를 활성화하므로 **확장성**과 **가용성** 확보 가능

```terraform
resource "aws_lb" "example" {
  name               = "terraform-asg-example"
  load_balancer_type = "application"
  subnets            = data.aws_subnet_ids.default.ids
}
```

### ALB 리스너 생성
```terraform
resource "aws_lb_listener" "http" {
  load_balancer_arn = aws_lb.example.arn
  port              = 80
  protocol          = "HTTP"

  # 기본값으로 단순한 404 페이지 오류 반환
  default_action {
    type = "fixed-response"

    fixed_response {
      content_type = "text/plain"
      message_body = "404: page not found"
      status_code  = 404
    }
  }
}
```

기본 HTTP 포트인 80번 포트를 수신하고, HTTP를 프로토콜로 사용하고,  
리스너 규칙과 일치하지 않는 요청에 대해 기본 응답으로 404 페이지를 보내도록 구성

### ALB 보안 그룹
```terraform
resource "aws_security_group" "alb" {
  name = "terraform-example-alb"

  # 인바운드 HTTP 트래픽 허용
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  # 모든 아웃바운드 트래픽 허용
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}
```

80번 포트에서 들어오는 요청을 허용하여 HTTP를 통해 로드 밸런서에 접속 가능  
바깥으로 나가는 요청은 포트와 상관없이 허용하여 로드 밸런서가 '상태 확인(health check)'을 수행하도록 구성

#### ALB 수정
ALB가 보안 그룹을 사용할 수 있도록 ALB에 보안 그룹 지정

```terraform
resource "aws_lb" "example" {
  name               = "terraform-asg-example"
  load_balancer_type = "application"
  subnets            = data.aws_subnet_ids.default.ids
  security_groups    = [aws_security_group.alb.id]
}
```

### ALB 타겟 그룹 생성
```terraform
resource "aws_lb_target_group" "asg" {
  name = "terraform-asg-example"
  port     = var.server_port
  protocol = "HTTP"
  vpc_id   = data.aws_vpc.default.id

  health_check {
    path                = "/"
    protocol            = "HTTP"
    matcher             = "200"
    interval            = 15
    timeout             = 3
    healthy_threshold   = 2
    unhealthy_threshold = 2
  }
}
```

각 인스턴스에 주기적으로 HTTP 요청을 전송하여 인스턴스 상태를 점검하고,  
구성 `matcher`와 일치하는 응답을 반환하는 경우에만 인스턴스를 '정상(healthy)'로 간주

인스턴스가 다운되었거나 오버로드되어 응답하지 않으면 '비정상(unhealthy)'으로 표시되고  
대상 그룹은 사용자가 받는 지장을 최소화하기 위해 트래픽 전송을 자동으로 중지

#### ASG에 타겟 그룹 지정
