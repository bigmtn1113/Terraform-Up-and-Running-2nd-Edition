# 로드 밸런서 배포

<br>

각각 고유한 IP 주소를 가진 서버가 여러 개 있지만 사용자에게는 일반적으로 하나의 IP 주소를 제공  
로드 밸런서를 배포하여 서버 전체에 트래픽을 분산시키고 모든 사용자에게 로드 밸런서 IP를 제공

<br>

## 로드 밸런서 유형
- **Application Load Balancer(ALB)**
  - HTTP 및 HTTPS 트래픽 처리에 적합한 로드 밸런서
  - OSI 모델의 응용(7) 계층에서 작동
- **Network Load Balancer(NLB)**
  - TCP, UDP 및 TLS 트래픽 처리에 적합한 로드 밸런서
  - ALB보다 빠르게 로드에 응답하여 확장 및 축소 가능
  - 초당 수천만 개의 요청 처리 가능
  - OSI 모델의 전송(4) 계층에서 작동
- **Classic Load Balancer(CLB)**
  - '레거시' 로드 밸런서
  - HTTP, HTTPS, TCP 및 TLS 트래픽 처리가 가능하나, ALB 또는 NLB 보다 기능을 적게 보유
  - OSI 모델의 응용(7) 계층 및 전송(4) 계층에서 모두 작동

대부분의 응용 프로그램은 ALB 또는 NLB를 사용하며 현재 학습 예제는 HTTP 앱이므로 ALB가 가장 적합

<br>

## Application Load Balancer
### 구성
- **리스너(Listener)**
  - 80 같은 특정 포트와 HTTP 같은 프로토콜 수신
- **리스너 규칙(Listener rule)**
  - 리스너에 들어오는 요청을 가져와 /foo 및 /bar 같은 특정 경로나  
    foo.example.com 및 bar.example.com 같은 호스트 이름과 일치하는 요청을 특정 대상 그룹으로 전송
- **대상 그룹(Target groups)**
  - 로드 밸런서에서 요청을 받는 하나 이상의 서버
  - 서버의 상태를 확인하고 요청을 정상 노드로 전송

### ALB 생성
AWS 로드 밸런서는 단일 서버가 아니라 별도의 서브넷(및 별도의 데이터 센터)에서 실행될 수 있는 여러 서버로 구성  
AWS는 트래픽에 따라 로드 밸런서 서버 수를 자동으로 확장 또는 축소하고  
해당 서버 중 하나가 다운되면 장애 조치를 활성화하므로 **확장성**과 **가용성** 확보 가능

```terraform
resource "aws_lb" "example" {
  name               = "terraform-asg-example"
  load_balancer_type = "application"
  subnets            = data.aws_subnet_ids.default.ids
}
```

### ALB 리스너 생성
```terraform
resource "aws_lb_listener" "http" {
  load_balancer_arn = aws_lb.example.arn
  port              = 80
  protocol          = "HTTP"

  # 기본값으로 단순한 404 페이지 오류 반환
  default_action {
    type = "fixed-response"

    fixed_response {
      content_type = "text/plain"
      message_body = "404: page not found"
      status_code  = 404
    }
  }
}
```

기본 HTTP 포트인 80번 포트를 수신, HTTP를 프로토콜로 사용, 리스너 규칙과 일치하지 않는 요청에 대해 기본 응답으로 404 페이지를 보내도록 구성

### ALB 보안 그룹
```terraform
resource "aws_security_group" "alb" {
  name = "terraform-example-alb"

  # 인바운드 HTTP 트래픽 허용
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  # 모든 아웃바운드 트래픽 허용
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}
```

80번 포트에서 들어오는 요청을 허용하여 HTTP를 통해 로드 밸런서에 접속 가능  
바깥으로 나가는 요청은 포트와 상관없이 허용하여 로드 밸런서가 '상태 확인(health check)'을 수행하도록 구성

#### ALB 수정
ALB가 보안 그룹을 사용할 수 있도록 ALB에 보안 그룹 지정

```terraform
resource "aws_lb" "example" {
  name               = "terraform-asg-example"
  load_balancer_type = "application"
  subnets            = data.aws_subnet_ids.default.ids
  security_groups    = [aws_security_group.alb.id]
}
```

### ALB 대상 그룹 생성
```terraform
resource "aws_lb_target_group" "asg" {
  name     = "terraform-asg-example"
  port     = var.server_port
  protocol = "HTTP"
  vpc_id   = data.aws_vpc.default.id

  health_check {
    path                = "/"
    protocol            = "HTTP"
    matcher             = "200"
    interval            = 15
    timeout             = 3
    healthy_threshold   = 2
    unhealthy_threshold = 2
  }
}
```

각 인스턴스에 주기적으로 HTTP 요청을 전송하여 인스턴스 상태를 점검하고,  
구성 `matcher`와 일치하는 응답을 반환하는 경우에만 인스턴스를 '정상(healthy)'로 간주

인스턴스가 다운되었거나 오버로드되어 응답하지 않으면 '비정상(unhealthy)'으로 표시되고  
대상 그룹은 사용자가 받는 지장을 최소화하기 위해 트래픽 전송을 자동으로 중지

#### ASG에 대상 그룹 지정
대상 그룹이 어느 EC2 인스턴스에 요청을 보내야 할지 식별

`aws_lb_target_group_attachment` 리소스를 사용하면 EC2 인스턴스의 정적 목록을 대상 그룹에 연결 가능  
그러나 ASG를 사용하면 정적 목록이 작동하지 않는데 이는 언제든지 인스턴스를 시작하거나 종료할 수 있기 때문

ASG와 ALB를 통합하는 방법 사용  
```terraform
resource "aws_autoscaling_group" "example" {
  launch_configuration = aws_launch_configuration.example.name
  vpc_zone_identifier  = data.aws_subnet_ids.default.ids

  target_group_arns = [aws_lb_target_group.asg.arn]
  health_check_type = "ELB"

  min_size = 2
  max_size = 10

  tag {
    key                 = "Name"
    value               = "terraform-asg-example"
    propagate_at_launch = true
  }
}
```

※ `health_check_type`  
기본 `health_check_type`은 `EC2`인데, 이는 AWS 하이퍼바이저가 VM이 완전히 다운되었거나 도달할 수 없다고 판단하는 경우에만  
인스턴스가 비정상 상태라고 간주하는 최소한의 상태 확인
 
`ELB` 상태 점검은 ASG가 대상 그룹의 상태 확인을 하여 인스턴스가 정상인지 여부를 판단하고  
대상 그룹이 상태 불량으로 보고되면 인스턴스를 자동으로 교체하도록 지시하므로 더욱 강력
 
인스턴스가 완전히 다운되었을 때분 아니라 메모리 부족으로 인해 요청 처리가 중단되거나  
중요한 프로세스가 중단되는 경우에도 인스턴스를 교체
 
### ALB 리스너 규칙
```terraform
resource "aws_lb_listener_rule" "asg" {
  listener_arn = aws_lb_listener.http.arn
  priority     = 100

  condition {
    path_pattern {
      values = ["*"]
    }
  }

  action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.asg.arn
  }
}
```

모든 경로와 일치하는 요청을 ASG가 포함된 대상 그룹으로 보내는 리소스 규칙 추가

### outputs 파일 수정
단일 EC2 인스턴스의 기존 `public_ip` 출력을 ALB의 DNS 이름을 표시하는 출력으로 변경

outputs.tf 파일 수정  
```terraform
output "alb_dns_name" {
  value       = aws_lb.example.dns_name
  description = "The domain name of the load balancer"
}
```

`terraform apply` 명령어 실행 후, Outputs 확인
```bash
$ terraform apply
```

<br>

## 결과 확인
### ASG
![image](https://user-images.githubusercontent.com/46125158/205639032-0e156ee4-40ae-4b45-8bd9-0c0f26dd50c6.png)

### EC2 인스턴스
![image](https://user-images.githubusercontent.com/46125158/205639136-0aab83e6-e453-4d76-bdfb-563fe8df2172.png)

### ALB
![image](https://user-images.githubusercontent.com/46125158/205639220-eb79af01-2245-4b2d-b38e-042c830f4d8a.png)

### 대상 그룹
![image](https://user-images.githubusercontent.com/46125158/205639338-661bd016-6a23-4b08-b684-1231e87f5b20.png)

### 서버 작동 테스트
Outputs로 출력된 URL로 curl 명령어 수행

```bash
$ curl http://<alb_dns_name>
Hello, World
```
