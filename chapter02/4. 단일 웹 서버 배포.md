# 단일 웹 서버 배포

<br>

EC2 인스턴스에서 웹 서버 실행하는 것을 목표로 설정

<br>

## 웹 서버 실행 script
### script 설명
```bash
#!/bin/bash
echo "Hello, World" > index.html
nohup busybox httpd -f -p 8080 &
```

index.html 파일에 'Hello, World'라는 텍스트를 저장한 다음, busybox라는 도구로 포트 8080에서 웹서버를 실행하여 해당 파일을 제공  
Busybox 명령을 nohup과 &로 래핑하여 배시 스크립트가 종료되더라도 웹 서버가 백그라운드에서 영구적으로 실행

#### ※ 포트번호가 8080인 이유
1024보다 숫자가 작은 포트에서 listen하려면 루트 사용자 권한 필요  
서버를 손상시키는 공격자가 루트 권한을 가질 수 있으므로 보안 위험 존재

루트 사용자가 아닌 권한이 제한된 다른 사용자로 웹 서버를 실행하는 것이 바람직  
즉, 더 높은 번호의 포트로 수신

<br>

## Terraform 코드 작성
### User data
EC2 인스턴스에서 script를 실행하려면 서버 템플릿 도구인 Packer와 같은 도구를 사용하여 웹 서버가 설치된 사용자 지정 AMI를 생성

해당 예제의 임시 웹 서버는 한 줄짜리 busybox 명령어로 되어 있으므로 기본 우분투 20.04 AMI를 사용할 수 있으며  
EC2 인스턴스의 user data를 설정하여 스크립트 실행 가능

EC2 인스턴스가 시작될 때, 셸 스크립트 또는 Windows의 경우 파워셸 명령어를 user data에 전달하여 특정 작업을 수행하도록 지시 가능

main.tf  
```terraform
resource "aws_instance" "example" {
  ami                    = "ami-0fb653ca2d3203ac1"
  instance_type          = "t2.micro"
  
  user_data = <<-EOF
              #!/bin/bash
              echo "Hello, World" > index.html
              nohup busybox httpd -f -p 8080 &
              EOF

  tags = {
    Name = "terraform-example"
  }
}
```

`<<-EOF` 및 `EOF`는 Terraform의 히어닥(heredoc) 구문을 이용해 줄 바꿈 문자를 삽입하지 않고도 여러 줄로 된 코드 작성 가능

### Security group 설정
기본적으로 AWS는 EC2 인스턴스에 들어오거나 나가는 트래픽을 허용하지 않으므로  
EC2 인스턴스가 8080 포트에서 트래픽을 수신하도록 하려면 보안 그룹 생성 필요

main.tf  
```terraform
resource "aws_security_group" "instance" {
  name = "terraform-example-instance"

  ingress {
    from_port   = 8080
    to_port     = 8080
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
}
```

CIDR 블록 0.0.0.0/0은 가능한 모든 IP 주소를 포함하는 IP 주소 범위이므로 해당 설정은 모든 IP의 8080 포트로 들어오는 TCP 요청을 허용

보안 그룹을 생성했으면 EC2 인스턴스가 이 보안 그룹을 사용할 수 있도록 하는 작업 필요  
보안 그룹의 ID를 aws_instance 리소스의 vpc_security_group_ids 인수에 저장

main.tf  
```terraform
resource "aws_instance" "example" {
  ami                    = "ami-0fb653ca2d3203ac1"
  instance_type          = "t2.micro"
  vpc_security_group_ids = [aws_security_group.instance.id]

  user_data = <<-EOF
              #!/bin/bash
              echo "Hello, World" > index.html
              nohup busybox httpd -f -p 8080 &
              EOF

  tags = {
    Name = "terraform-example"
  }
}
```

#### ※ 참조(reference) 표현식
코드의 다른 부분에서 값을 액세스할 수 있게 해주는 표현식

```terraform
# PROVIDER: 공급자 이름
# TYPE: 리소스 유형
# NAME: 리소스 이름
# ATTRIBUTE: 리소스의 인수 중 하나이거나 리소스가 내보낸 속성 중 하나

<PROVIDER>_<TYPE>.<NAME>.<ATTRIBUTE>
aws_security_group.instance.id
```
